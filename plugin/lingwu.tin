#class lingwu open
#tab lw.start;
#tab lw.stop;

#alias {lw.start} {
    set_last_quest lw;
    #alias {lw_action} {#cr;};
    #class lingwu.inner open;
    #list skills create  {force;cuff;strike;whip;staff;sword;parry;claw;blade;spear;dodge;hammer;hand;finger;dagger;throwing};
    #list skills size skill_size;
    #list level_a create {};
    #list level_b create {};
    #list level_a clear;
    #list level_b clear;
    #loop 1 $skill_size tmp {#list level_b add 0;#list level_a add 0};
    #var skill_index 1;
    #list skills get $skill_index skill;

    #action {(%*)%s： %S[互备]%s有效等级：} {
        #var basic_skill %%1;
        #var special_skill %%3;
        #list special_skills find ${special_skill} tmp;
        #if {${tmp} != 0} {
            #list special_skills_level get ${tmp} tmp_level;
            record_level_b ${basic_skill} ${tmp_level};
        };
    } {1};

    #action {(%*)%s： %S%s有效等级：} {
        #var basic_skill %%1;
        #var special_skill %%3;
        #list special_skills find ${special_skill} tmp;
        #if {${tmp} != 0} {
            #list special_skills_level get ${tmp} tmp_level;
            record_level_b ${basic_skill} ${tmp_level};
        };
    } {2};

    #action {│%s%S%s│%S%s│%S│%s%d│%*} {
        #var basic_skill %%4;
        #var basic_skill_level %%8;
        record_level_a  ${basic_skill} ${basic_skill_level};
    } {3};

    #action {│□%S%s│%S%s│%S│%s%d│%*} {
        #var special_skill %%1;
        #var special_skill_level %%7;
        #list special_skills add ${special_skill};
        #list special_skills_level add ${special_skill_level};
    } {2};

    #action {你的技能等级最多能达到%*级} {
        #var max_level @ctd{%%1};
    };

    #alias {do_lingwu} {
        lingwu $skill 100;
    };

    #alias {do_lian} {
        lian $skill 50;
    };

    #alias {get_level_a} {
        #var skill_level_a 0;
        #list skills find %%1 tmp_index;
        #if {$tmp_index != 0} {
            #list level_a get $tmp_index tmp;
            #var skill_level_a $tmp;
        };
    };

    #alias {get_level_b} {
        #var skill_level_b 0;
        #list skills find %%1 tmp_index;
        #if {$tmp_index != 0} {
            #list level_b get $tmp_index tmp;
            #var skill_level_b $tmp;
        };
    };

    #alias {record_level_a} {
        #list skills find %%1 tmp_index;
        #if {$tmp_index != 0} {
            #list level_a set $tmp_index %%2;
        };
    };

    #alias {record_level_b} {
        #list skills find %%1 tmp_index;
        #if {$tmp_index != 0} {
            #list level_b set $tmp_index %%2;
        };
    };

    #alias {start_ticker} {
        #ticker {lw_ticker} {
            #if {${nl_percent} < 10} {
                stop_ticker;
                on_there {
                    sleep;
                };
                goto_rest;
                on_wakeup {
                    chifan;
                    on_there {
                        start_ticker;
                    };
                    goto_lingwu;
                };
            };
            #else {
                lw_action;
            };
        } {1};
    };

    #alias {stop_ticker} {
        #unticker {lw_ticker};
    };

    #action {必须空手} {
        unwi;
    };

    #action {你的基本功夫比你的高级功夫还高} {
        #if {"${skill}" == "force"} {
            #math skill_index ${skill_index}+1;
        };
        lw.restart;
    };

    #action {你需要提高基本功} {
        lw.restart;
    };

    #alias {lw_next} {
        stop_ticker;
        #math skill_index ${skill_index}+1;
        #if {$skill_index <= $skill_size} {
            lw.restart;
        };
        #else {
            lw.stop;
            #showme MISSION_COMPLETE: lw;
        };
    };

    #action {必须使用匕首类武器} {
        wield dagger;
    };

    #list {lw_next_actions} create {
        道德小于零才能练这种阴毒功夫;
        乾坤大挪移心法只能用学习;
        你使用的武器不对;
        火候太浅;
        你不能练;
        特殊内功无法练习;
        你现在只能在岳王墓中观看墙壁才能继续提高岳家枪法;
        你要装备 %* 类武器才能练习;
        你必须先找一杆枪才能练此枪法;
        你还没学过这项技能吧;
        玄铁剑法只能是由杨过进行点拨或亲自到大海中才能锻炼提高！
    };

    #foreach {${lw_next_actions}[%*]} {lw_next_action} {
        #action {${lw_next_action}} {
            lw_next;
        } {1};
    };
    #action {你的「%*」进步了！} {
        lw.restart;
    };

    #class lingwu.inner close;
    show_to_quest 领悟;
    on_there {
        lw.restart;
    };
    goto_lingwu;
};

#alias {lw.stop} {
    unset_last_quest;
    stop_ticker;
    #undelay {lw.restart};
    #class lingwu.inner kill;
    show_to_quest 领悟结束;
};

#alias {lw.restart} {
    stop_ticker;
    burden.check {
        lw.restart.inner;
    };
};

#alias {lw.restart.inner} {
    #if {@getburden{酥油茶} < 30} {
        lw.stop;
        chain get.qian get.fan lw.start;
        #return;
    };
    #list special_skills clear;
    #list special_skills_level clear;
    #send {sk};
    #delay {1} {#send {jifa}};
    #list skills get $skill_index skill;
    #delay {lw.restart} {
        get_level_a $skill;
        get_level_b $skill;
        #if {${skill_level_a == 0} || ${skill_level_b} == 0} {
            #showme {火候太浅};
            #return;
        };
        show_to_quest 领悟 $skill: $skill_level_a / $skill_level_b -> ${max_level};
        #if {$skill_level_a >= $max_level && $skill_level_b >= $max_level} {
            #showme {火候太浅};
            #return;
        };

        unwi;
        wield ${skill};

        #if {$skill_level_a <= $skill_level_b} {
            #alias {lw_action} {do_lingwu};
        };
        #else {
            #alias {lw_action} {do_lian};
        };

        start_ticker;
    } {2};
};
#class lingwu close