#class guaji open
#var guaji_item nil;
#alias {guatieqiao.start} {
    #var tq_count 0;
    #class guatieqiao.inner open;
    #action {(%*)告诉你：tq} {
        burden.check;
        #action {burden.check: done} {
            #unaction {burden.check: done};
            tell %%1 现在有 @getburden{铁锹} 柄铁锹;
            give %%1 tie qiao;
        };
    } {1};

    #alias {ticker.gua.start} {
        kill gaoshou;
        on_dead {
            on_unbusy {
                get tie qiao from corpse;
            };
        };
    };

    #alias {ticker.gua.end} {
        #cr;
    };

    #class guatieqiao.inner close;
    guaji.start tieqiao;
};

#alias {guatieqiao.stop} {
    #class guatieqiao.inner kill;
    guaji.stop;
};

#alias {guasan.start} {
    set_last_quest guasan;
    #var san_count 0;
    #class guasan.inner open;
    #alias {san_report} {
        #alias {burden_check_done} {
            show_to_quest 清心散: @getburden{清心散};
        };
        burden.check {
            burden_check_done;
        };
    };
    #ticker {san_report} {
      san_report;
    } {300};
    #action {%*(%*)告诉你：san} {
        #var dummy @lower{%%2};
        #if {@ismember{${dummy};dummy_list} == 0} {
            #return;
        };
        #alias {burden_check_done} {
            tell ${dummy} 现在有 @getburden{清心散} 包清心散;
            give ${dummy} qingxin san;
            #nop show_to_info %%1: @getburden{清心散} 包清心散;
        };
        burden.check {
            burden_check_done;
        };
    } {1};

    #alias {ticker.gua.start} {
        #ticker {gua} {
            get qingxin san;
        } {1}
    };

    #alias {ticker.gua.end} {
        #unticker {gua};
    };

    #class guasan.inner close;

    gt 星宿派储藏室;
    on_there {
        san_report;
        guaji.start qingxin san;
    };
};

#alias {guasan.stop} {
    unset_last_quest;
    #class guasan.inner kill;
    guaji.stop;
};

#alias {guaji.start} {
    #var guaji_item %0;
    show_to_quest 挂机: %0;
    #class guaji.inner open;
    #action {guaji.update %*} {
        #var tmp_minute @ctd{%%1};
        #if {${tmp_minute} == 59 || ${tmp_minute} == 29 || ${tmp_minute} == 14 || ${tmp_minute} == 44} {
            ticker.gua.start;
        };

        #if {${tmp_minute} == 1 || ${tmp_minute} == 31 || ${tmp_minute} == 16 || ${tmp_minute} == 46} {
            ticker.gua.end;
        };
    };

    #class guaji.inner close;
};

#alias {guaji.stop} {
    #var guaji_item nil;
    #class guaji.inner kill;
    show_to_quest 挂机结束;
};

#alias {guaxt.start} {
    set_last_quest guaxt;
    #class xtj_inner open;
    #action {拿到玄铁剑} {
        cun_xtj;
    };

    #alias {cun_xtj} {
        gt wm;
        on_there {
            put xuantie sword in xiang;
            chat 玄铁剑;
            #delay {1200} {
                quit_and_restart;
            };
        };
    };

    #action {玄铁剑没了} {
        #delay {1200} {
            quit_and_restart;
        };
    };
    #class xtj_inner close;
    show_to_quest 开始挂玄铁剑;
    get.xuan_tie_jian;
};

#alias {guaxt.stop} {
    unset_last_quest;
    show_to_quest 挂玄铁剑结束;
};
#class guaji close