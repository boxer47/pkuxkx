#class gem open
#tab gem.combine;
#tab gem.merge;

@mapcreate{gem_combine_level};
@mapset{gem_combine_level;玄武甲;6};
@mapset{gem_combine_level;麒麟角;6};
@mapset{gem_combine_level;凤凰羽;6};
@mapset{gem_combine_level;神龙骨;6};
#var gem_combine_level_default 7;

#list gem_types create {地;山;水;风;雷;火;泽;天};
#var gem.combine_count 0;
#alias {gem} {
    #var gem_filter nil;
    #var gem_filter %1;
    #var gem_value 0;
    #list gem_filter_list create {};
    #alias {check_gem} {
        #var gem_id %%1;
        #var gem_type %%2;
        #var gem_property %%3;
        #var gem_name %%4;
        #var gem_count %%5;
        #list gem_types find ${gem_type} gem_type_index;
        #math gem_value ${gem_value}+@power{3;${gem_type_index}}/3*${gem_count};
        #if {"${gem_filter}" != "nil"} {
            #if {@issubstring{${gem_type}${gem_property}${gem_name};${gem_filter}} == 1} {
                #list gem_filter_list add (${gem_id})${gem_type}${gem_property}${gem_name}*${gem_count};
            };
        };
    };
    gem.setup;
    jiancha jin nang;
    lock 用pack <物品id>将宝石放入其中;
    #action {用pack <物品id>将宝石放入其中} {
        #unaction {用pack <物品id>将宝石放入其中};
        gem.teardown;
        show_to_info 宝石价值: ${gem_value};
        #if {"${gem_filter}" != "nil"} {
            #foreach {${gem_filter_list}[%*]} {gem_item} {
                #echo ${gem_item};
            };
        };
    };
};

#function {gemlevel} {
    #list gem_types find %1 result;
};

#alias {gem.setup} {
    #class gem.inner open;
    #action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
        check_gem {%%2} {%%3} {%%4} {%%5} {%%6};
        check_gem {%%9} {%%10} {%%11} {%%12} {%%13};
        check_gem {%%16} {%%17} {%%18} {%%19} {%%20};
        check_gem {%%23} {%%24} {%%25} {%%26} {%%27};
        check_gem {%%30} {%%31} {%%32} {%%33} {%%34};
    } {1};

    #action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
        check_gem {%%2} {%%3} {%%4} {%%5} {%%6};
        check_gem {%%9} {%%10} {%%11} {%%12} {%%13};
        check_gem {%%16} {%%17} {%%18} {%%19} {%%20};
        check_gem {%%23} {%%24} {%%25} {%%26} {%%27};
    } {2};

    #action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
        check_gem {%%2} {%%3} {%%4} {%%5} {%%6};
        check_gem {%%9} {%%10} {%%11} {%%12} {%%13};
        check_gem {%%16} {%%17} {%%18} {%%19} {%%20};
    } {3};

    #action {(%s%S)%*{◎|☆|★}%*\*%S%s(%s%S)%*{◎|☆|★}%*\*%S} {
        check_gem {%%2} {%%3} {%%4} {%%5} {%%6};
        check_gem {%%9} {%%10} {%%11} {%%12} {%%13};
    } {4};

    #action {(%s%S)%*{◎|☆|★}%*\*%S} {
        check_gem {%%2} {%%3} {%%4} {%%5} {%%6};
    } {5};
    #class gem.inner close;
};

#alias {gem.teardown} {
    #classs gem.inner kill;
};

#alias {gem.combine} {
    #alias tmp_combine #cr;
    #alias tmp_combine %1;
    #alias {check_gem} {
        #var gem_id %%1;
        #var gem_type %%2;
        #var gem_property %%3;
        #var gem_name %%4;
        #var gem_count %%5;
        #list gem_types find ${gem_type} gem_type_index;
        #if {${gem_count} >= 3} {
            #var gem_level @mapget{gem_combine_level;${gem_name}};
            #if {"${gem_level}" == "nil"} {
                #var gem_level ${gem_combine_level_default};
            };
            #if {${gem_type_index} < ${gem_level}} {
                #var target_gem_id ${gem_id};
            };
        };
    };

    gem.setup;
    #class gem.combine.inner open;
    #action {笨阿，身上东西满了，小心无欢打劫你的宝石！} {
        #delay {gem_combine_delay} {
            combine_gems_done;
        } {0.5};
    };
    #action {你的宝石袋里空空荡荡} {
        combine_gems_done;
    };

    #action {用pack <物品id>将宝石放入其中} {
        combine_gems;
    };

    #alias {combine_gems_done} {
        #class gem.combine.inner kill;
        gem.teardown;
        #show 合成完毕;
        #delay {3} {
            tmp_combine;
        };
    };

    #alias {combine_gems_prepare} {
        #var target_gem_id -1;
        #3 {pack gem};
        #var gem.combine_count 0;
        jiancha jin nang;
    };

    #alias {combine_gems} {
        #if {${target_gem_id} != -1} {
            #3 {pickgem ${target_gem_id}};
            on_unbusy {
                combine gem;
                on_unbusy {
                    pack gem;
                    #if {${nl_percent} > 20} {
                        on_unbusy {
                            combine_gems_prepare;
                        };
                    };
                    #else {
                        combine_gems_done;
                    };
                };
            };
        };
        #else {
            combine_gems_done;
        };
    };
    #class gem.combine.inner close;

    combine_gems_prepare;
};

#alias {gem.transfer} {
    #var receiver %1;
    #class gem.transfer.inner open;
    #action {你的宝石袋里空空荡荡} {
        #class gem.transfer.inner kill;
    };
    #action {你给%*一颗} {
        pickgem 1;
    };
    #action {你从宝石袋里面取出} {
        #delay {1} {
            give ${receiver} gem;
        };
    };
    #action {这里没有 ${receiver}} {
        show_to_info 附近没有 ${receiver};
        #class gem.transfer.inner kill;
        pack gem;
    };
    #class gem.transfer.inner close;
    pickgem 1;
};

#alias {gem.merge} {
    #alias tmp_merge #cr;
    #alias tmp_merge %1;
    #class gem.merge.inner open;
    #alias {check_gem} {
        #var gem_id %%1;
        #var gem_type %%2;
        #var gem_yinyang %%3;
        #var gem_name %%4;
        #var gem_count %%5;

        #if {${gem_id_1} != -1} {
            #return;
        };

        #if {"${gem_name}" == "神龙骨" || "${gem_name}" == "凤凰羽" || "${gem_name}" == "玄武甲" || "${gem_name}" == "麒麟角" } {
            #var key ${gem_type}${gem_yinyang}${gem_name};
            @mapset{gem_mark;$key;${gem_id}};
            #if {"${gem_yinyang}" == "☆"} {
                #var rev_gem_yinyang ★;
            };
            #elseif {"${gem_yinyang}" == "★"} {
                #var rev_gem_yinyang ☆;
            };
            #else {
                #var rev_gem_yinyang n;
            };
            #var rev_key ${gem_type}${rev_gem_yinyang}${gem_name};
            #var rev_gem_id @mapget{gem_mark;${rev_key}};
            #if {"${rev_gem_id}" != "nil"} {
                #if {${gem_id} > ${rev_gem_id}} {
                    #var gem_id_1 ${gem_id};
                    #var gem_id_2 ${rev_gem_id};
                };
                #else {
                    #var gem_id_2 ${gem_id};
                    #var gem_id_1 ${rev_gem_id};
                };
            };
        };
    };
    #action {用pack <物品id>将宝石放入其中} {
        #unaction {用pack <物品id>将宝石放入其中};
        #class gem.merge.inner kill;
        on_unbusy {
            merge_gems;
        };
    };
    #class gem.merge.inner close;

    #alias {merge_gems} {
        #if {${gem_id_1} != -1} {
            pickgem ${gem_id_1};
            pickgem ${gem_id_2};

            on_unbusy {
                merge gem;
                on_unbusy {
                    pack gem;
                    #if {${nl_percent} > 20} {
                        on_unbusy {
                            gem.merge;
                        };
                    };
                    #else {
                        show_to_info 内力不足, 无法 merge gem;
                        #delay {3} {
                            tmp_merge;
                        };
                    };
                };
            };
        };
        #else {
            show_to_info 合成完毕;
            #delay {3} {
                tmp_merge;
            };
        };
    };

    #var gem_id_1 -1;
    #var gem_id_2 -1;
    @mapcreate{gem_mark};

    #nop #3 {pack gem};
    jiancha jin nang;
};

#alias {gem.record} {
    #var gem_level @gemlevel{%1};
    #if {${gem_level} >= 6} {
        notify %1%2%3;
        play gem2.wav;
    };
};

#alias {gem.pick} {
    #if {"%1" == ""} {
        #return;
    };
    #var gem_filter nil;
    #var gem_filter %1;

    #class gem.pick.inner open;
    #var gem_id_to_pick -1;
    #var gem_id_to_pick_count -1;
    #alias {check_gem} {
        #var gem_id %%1;
        #var gem_type %%2;
        #var gem_property %%3;
        #var gem_name %%4;
        #var gem_count %%5;
        #list gem_types find ${gem_type} gem_type_index;
        #if {@issubstring{${gem_type}${gem_property}${gem_name};${gem_filter}} == 1 && ${gem_id_to_pick} == -1} {
            #var gem_id_to_pick ${gem_id};
            #var gem_id_to_pick_count ${gem_count};
        };
    };
    #action {用pack <物品id>将宝石放入其中} {
        #if {${gem_id_to_pick} == -1} {
            gem.teardown;
            #class gem.pick.inner kill;
            show_to_info gem.pick 完成;
        };
        #else {
            pick_one_gem;
        };
    };
    #action {你从宝石袋里面取出一颗} {
        on_unbusy {
            pick_one_gem;
        };
    };
    #alias {pick_one_gem} {
        #if {${gem_id_to_pick_count} > 0} {
            #math gem_id_to_pick_count ${gem_id_to_pick_count}-1;
            pickgem ${gem_id_to_pick};
        };
        #else {
            gem.pick ${gem_filter};
        };
    };
    #class gem.pick.inner close;
    gem.setup;
    jiancha jin nang;
};
#class gem close