#class xuantie open
#tab xt.start;
#tab xt.stop;

#alias {xt.start} {
    set_last_quest xt;
    show_to_quest 江中练剑;
    #send {jifa sword xuantie-jian};
    #class xuantie.inner open;

    #action {□玄铁剑法 (xuantie-jian)%*  %d/} {
        #var xuantie_level %%2;
    };

    #action {你的技能等级最多能达到%*级} {
        #var max_level @ctd{%%1};
    };

    #alias {check_xuantie_level} {
        #send {sk};
        #delay {1} {
            #if {${max_level} <= ${xuantie_level}} {
                #showme MISSION_COMPLETE: xt;
                xt.stop;
            };
            #else {
                show_to_quest 江中练剑: ${xuantie_level} -> ${max_level};
            };
        };
    };

    #ticker {check_xuantie_level} {
        check_xuantie_level;
    } {300};

    #alias {start_lian} {
        #ticker {check} {
            #if {${hpbrief_curr_nl} < 1000 || ${hpbrief_curr_qx} < 300 || ${hpbrief_curr_js} < 300} {
                #unticker {check};
                halt;
                jump out;
                chifan;
                on_health {
                    start_lian;
                };
            };
        } {1};
        unwi;
        wield sword;
        jump jiang;
        lianjian;
    };

    #ticker {xuantie} {
        start_lian;
    } {400};

    #action {你见海潮退去} {
        #delay {5} {
            lianjian;
        };
    };
    #class xuantie.inner close;
    #alias {burden_check_done} {
        #if {@getburden{玄铁剑} == 0} {
            xt.stop;
            #showme MISSION_COMPLETE: xt;
        };
        #else {
            gt 钱塘江口;
            on_there {
                check_xuantie_level;
                start_lian;
            };
        };
    };
    burden.check {
        burden_check_done;
    };
};

#alias {xt.stop} {
    #class xuantie.inner kill;
    #unticker {check};
    unset_last_quest;
    health.cancel;
    halt;
    on_unbusy {
        jump out;
    };
    show_to_quest 练剑结束;
};

#class xuantie close