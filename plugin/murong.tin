#class murong open;
#tab mr.start;
#tab mr.stop;

#alias {mr.start} {
    #class mr.inner open;
    #action {%*点实战经验} {
        show_to_info @ctd{%%1} 点经验;
    } {1};

    #action {姑苏慕容叛徒%*气血} {
        #class mr.bl.inner kill;
        #undelay {mr_fight_delay};
    } {1};

    #action {一脚踢向你的屁股} {
        mr.f;
    };

    #action {由于你没有找回大燕传国玉玺} {
        on_health {
            mr.q;
        };
    };

    #action {你身上没有 xi 这样东西。} {
        mr.f;
    };

    #action {由于你成功的找回大燕传国玉玺} {
        show_to_quest 慕容任务: 完成;
        mr.q;
    };

    #action {大燕传国玉玺在以下地点附近出现} {
        #unticker {mr_q_ticker};
        show_to_quest 慕容任务: 未知;
    };

    #action {慕容复说道：大燕传国玉玺在『%*』附近出现，快去吧！} {
        #unticker {mr_q_ticker};
        show_to_quest 慕容任务: %%1;
        mr.check %%1;
    } {1};

    #action {慕容复忙着呢} {
        #delay {2} {
            mr.d;
        };
    };

    #action {姑苏慕容叛徒%*死了。} {
        #class mr.bl.inner kill;
        on_unbusy {
            get all from corpse;
        };
    } {1};

    #action {你从姑苏慕容%*搜出一块玉玺。} {
        show_to_quest 慕容任务: 收工;
        #delay {mr_delay} {
            mr.b;
        } {3};
    };

    #class mr.inner close;

    on_there {
        on_health {
            mr.q;
        };
    };
    gt mrf;
};

#alias {mr.check} {
    #var dest %1;
    gt $dest;
    on_there {
        mr.bl;
    };
    on_not_there {
        mr.f;
    };
};

#alias {mr.stop} {
    #class mr.inner kill;
    #unticker {mr_q_ticker};
    show_to_quest 慕容任务结束;
};

#alias {mr.q} {
    drop_garbage;
    #ticker {mr_q_ticker} {
        ask murong about job;
    } {5};
};

#alias {mr.d} {
    give murong xi;
};

#alias {mr.fight} {
    #class mr.bl.inner kill;
    #delay {mr_fight_delay} {
        mr.bl;
    } {10};

    on_unbusy {
        killall pantu;
    };
};

#alias {mr.bl} {
    #class mr.bl.inner open;
    #action {找到目标} {
        mr.fight;
    };

    #action {GPS: 遍历完成} {
        #class mr.bl.inner kill;
        mr.b;
    };
    #class mr.bl.inner close;
    bl.start ${char_name}%*叛徒;
};

#alias {mr.b} {
    #class mr.bl.inner kill;
    #undelay {mr_fight_delay};

    gt mrf;
    on_there {
        give xi to murong;
    };
};

#alias {mr.f} {
    show_to_quest 慕容任务: 失败;
    ask murong about fail;
};

#class murong cloe;