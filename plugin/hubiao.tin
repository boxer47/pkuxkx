#class hubiao open
#tab hb.start;
#tab hb.stop;

#var hb_max_path_size 45;
#var hb_auto 0;
#var hb_rotate 0;
#var hb_dest nil;
#var hb_paused 0;
#var hb_requested 0;
#var hb_info nil;
#var hb_rotate_location nil;
#var hb_rotate_location_count 0;
#var hb_robber_count 0;
#var hb_need_kill 1;

#var HB_GPS_WEIGHT "1,1,1,-1,-1,1,-1,-1";

@mapcreate{robber_score};
@mapset{robber_score;gaibang;9};
@mapset{robber_score;wudang;8};
@mapset{robber_score;taohua;7};
@mapset{robber_score;xingxiu;6};
@mapset{robber_score;huashan;5};
@mapset{robber_score;shaolin;4};
@mapset{robber_score;quanzhen;3};

#function {should_accept_quest} {
    #var hb_owner_loc @getenv{hb_owner_loc};
    #if {@ismember{%1;hb_blacklist:${hb_owner_loc}} != 0} {
        #return 0;
    };
    #if {"${xy_info}" == "on" && @ismember{%1;hb_blacklist_baowei:${hb_owner_loc}} != 0} {
        #return 0;
    };
    #return 1;
};

#alias {hb.watchdog.kick} {
    #delay {hb_check_delay} {
        hb.run;
    } {60};
};

#alias {hb.watchdog.stop} {
    #undelay {hb_check_delay};
};

#alias {hb.spec} {
    #alias tmp_hb_spec #cr;
    #alias tmp_hb_spec %1;
    tmp_hb_spec;
};

#alias {hb.teardown} {
    hb.watchdog.stop;
    #var quest nil;
    #var hb_robber_count 0;
    #class hb_setup_inner kill;
    #unticker {hb_ticker};
    set brief 2;
};

#alias {hb.traverse} {
    #alias {auto_pp_done} {
        #var gps_traverse_location %%0;
        #alias {gps_check_room_done} {
            #if {${gps.roomno} == -1} {
                #delay {1} {
                    notify 当前房间未记录, 无法遍历伙计;
                };
            };
            #else {
                INVOKE gps.traverse ${gps.roomno} "${gps_traverse_location}" "dfs";
                #var hb_path ${gps_path};
                #var hb_path_index 1;
                #list hb_path size hb_path_size;
                #var hb_paused 0;
                #if {${hb_path_size} == 0} {
                    #if {"${pos.room}" == "${gps_traverse_location}"} {
                        hb.back_and_forth;
                    };
                    #else {
                        notify 无法遍历伙计 @ ${gps_traverse_location};
                    };
                };
                #else {
                    hb.run;
                };
            };
        };
        #if {"${gps_traverse_location}" == "nil"} {
            show_to_info 伙计不存在;
            hb.b;
        };
        #else {
            gps.check_room {
                gps_check_room_done;
            };
        };
    };

    autopp {
        auto_pp_done;
    };
};

#alias {hb.run} {
    hb.watchdog.kick;
    #list hb_path get ${hb_path_index} hb_next_step;
    #if {@startwith{${hb_next_step};gps}} {
        #replace {hb_next_step} {gps} {hb};
    };
    #elseif {@ismember{${hb_next_step};directions} != 0} {
        #var hb_next_step gan che to ${hb_next_step};
    };
    #else {
        #var hb_next_step hb.command ${hb_next_step};
    };

    #if {${qx_percent} < 30 || ${js_percent} < 30 || ${nl_percent} < 10} {
        hb.pause;
        #delay {hb_move} {
            on_truce {
                on_health {hb.resume};
            };
        } {3};
    };
    #else {
        #if {${hb_path_index} > ${hb_path_size}} {
            hb.pause;
            #delay {hb_move} {
                #if {"@getenv{hb_state}" == "STARTED"} {
                    @setenv{hb_state;TRAVERSE};
                };
                #if {"@getenv{hb_state}" == "STARTED" || "@getenv{hb_state}" == "TRAVERSE"} {
                    hb.resume;
                };
            } {5};
        };
        #else {
            hb.before_move;
            #delay {hb_move} {
                #if {${hb_paused} == 0} {
                    ${hb_next_step};
                };
            } {3};
        };
    };
};

#alias {hb.before_move} {
    #if {${eff_qx_percent} < 95} {
        #2 yun heal;
    };
    #if {${eff_qx_percent} < 50} {
        #2 yun lifeheal ${char_id};
    };
    #if {${eff_js_percent} < 95} {
        #2 yun inspire;
    };
};

#var hb_retry_count 0;
#alias {hb.setup} {
    set brief 1;
    #var quest hb;
    #var hb_remaining_path_size ${hb_max_path_size};
    #var hb_path_index 1;
    #class hb_setup_inner open;
    #action {你已经在新手镖局获得足够经验了，快到大城市去闯荡一番吧。} {
        show_to_info 已经在新手镖局获得足够经验;
        hb.stop;
    };

    #alias {hb.guohe} {
        #class guohe.inner open;
        #ticker {sw/yell_boat} {
            yell boat;
        } {5};
        #action {正等着你呢，上来吧。} {
            gan che to enter;
        };
        #action {艄公将一块踏脚板搭上堤岸，} {
            gan che to enter;
        };
        #alias {shangchuan} {
            #unticker {sw/yell_boat};
            #action {所有人被赶下了渡船} {
                #unaction {所有人被赶下了渡船};
                #class guohe.inner kill;
                #math hb_path_index ${hb_path_index}+1;
                #var hb_paused 0;
                hb.run;
            };
        };
        #action {^黄河渡船$} {
            shangchuan;
        };
        #action {^长江渡船$} {
            shangchuan;
        };
        #class guohe.inner close;

        hb.pause;
        ask shao gong about huanghe;
        ask shao gong about jiang;
    };

    #alias {hb.command} {
        #var hb_command %%0;
        ${hb_command};
        #math hb_path_index ${hb_path_index}+1;
        hb.run;
    };

    #alias {hb.delay} {
        #var gps_delay 1.5;
        #var gps_delay %1;
        #delay {${gps_delay}} {
            #math hb_path_index ${hb_path_index}+1;
            hb.run;
        };
    };

    #alias {hb.busy} {
        on_unbusy {
            #math hb_path_index ${hb_path_index}+1;
            hb.run;
        };
    };

    #alias hb.clear hb.force_clear;
    #alias {hb.force_clear} {
        #var hb_tmp_blocker %%0;
        #class clear_blocker.inner open;
        #action {你想杀谁} {
            #class clear_blocker.inner kill;
            #math hb_path_index ${hb_path_index}+1;
            hb.run;
        };
        #ticker {tmp_blocker_ticker_kill} {
            killall ${hb_tmp_blocker};
        } {5};
        #class clear_blocker.inner close;

        killall ${hb_tmp_blocker};
    };

    #action {看来该找机会逃跑了} {
        hb.pause;
        show_to_tuiche 丢镖;
        notify 丢镖;
        hb.teardown;
        hb.b;
    };

    #action {^慢慢地你终于又有了知觉....} {
        show_to_tuiche 挂了;
        notify 挂了;
        hb.teardown;
        hb.b;
    };

    #action {^你的眼前一黑，接著什么也不知道了} {
        hb.pause;
    };

    #action {『劫匪%*气血%*』} {
        #nop #var hb_retry_count 0;
        #cr;
    } {1};

    #action {你想杀谁？} {
        hb.retry;
    };

    #action {镖车还没有跟上来呢,走慢点.} {
        hb.retry;
    };

    #alias {hb.maybe_kill} {
        #math hb_retry_count ${hb_retry_count}+1;
        #if {${hb_retry_count} > 10} {
            #var hb_retry_count 0;
            killall;
        };
    };

    #action {你还是先把对手解决了再说吧} {
        hb.maybe_kill;
        hb.retry;
    } {1};

    #action {镖车呼啦一声散了架} {
        hb.pause;
        notify 镖车呼啦一声散了架;
        hb.b;
    };

    #action {busy_for_move} {
        hb.retry;
    };

    #action {^你赶着镖车驶了过来。$} {
        #var hb_retry_count 0;
        #if {${hb_paused} == 0} {
            #math hb_path_index ${hb_path_index}+1;
            hb.run;
        };
    };

    #action {劫匪个起纵遁入暗里不见了} {
        hb.detect_enemy;
        hb.retry;
    };

    #action {^这个方向} {
        #if {${hb_paused} == 0} {
            hb.pause;
            hb.resume;
        };
    };

    #action {劫匪趁你不注意，推着镖车就跑，你赶紧追了上去。} {
        hb.pause;
        @setenv{hb_mixin_lunci;6};
        #delay {hb_delay} {
            hb.resume;
        } {3};
    };

    #action {劫匪伸手一拦道} {
        war;
        hb.maybe_kill;
        hb.retry;
    } {1};

    #action {你长笑一声，开始对周围众人痛下杀手！} {
        #var hb_need_kill 0;
    };

    #action {劫匪突然从暗处跳了出来} {
        hb.detect_enemy;
        #if {${hb_need_kill} == 1} {
            killall robber;
        };
        #else {
            war;
        };
    };

    #action {围上来几个伙计将镖车拉了进去} {
        @setenv{hb_state;ENDED};
        hb.pause;
        raw;
        show_to_tuiche 任务完成;
        hb.teardown;

        #alias {on_taopao_done} {
            #if {@getenv{hb_lunci} == @getenv{hb_mixin_lunci}} {
                @setenv{hb_lunci;0};
                chain hb.p hb.back fullme hb.d;
            };
            #else {
                hb.b;
            };
        };

        taopao {
            on_taopao_done;
        };
    } {1};

    #class hb_setup_inner close;

    #var hb_elapsed_time 0;
    #ticker {hb_ticker} {
        #math hb_elapsed_time ${hb_elapsed_time}+1;
        show_to_tuiche ${hb_info} --> ${hb_elapsed_time}:00;
    } {60};
};

#alias {hb.retry} {
    #if {${hb_paused} == 0} {
        #delay {hb_move} {
            hb.run;
        } {3};
    };
};

#alias {hb.pause} {
    hb.watchdog.stop;
    #var hb_paused 1;
    #undelay {hb_move};
};

#alias {hb.get_owner} {
    #alias tmp_hb_get_owner #cr;
    #alias tmp_hb_get_owner %1;

    #alias {get_pos_done} {
        @setenv{hb_owner_loc;${pos.room}};
        #if {"${pos.room}" == "昌隆镖局"} {
            @setenv{hb_owner;zuo erba};
        };
        #elseif {"${pos.room}" == "荆西镖局"} {
            @setenv{hb_owner;zhang zhi};
        };
        #elseif {"${pos.room}" == "飞虎镖局"} {
            @setenv{hb_owner;chu daye};
        };
        #elseif {"${pos.room}" == "天福镖局"} {
            @setenv{hb_owner;wang xiaoyue};
        };
        #else {
            @setenv{hb_owner;nil};
            @setenv{hb_owner_loc;nil};
        };
        tmp_hb_get_owner;
    };
    get_pos {
        get_pos_done;
    };
};

#alias {hb.d} {
    #var hb_state @getenv{hb_state};
    #if {"${hb_state}" == "GOT_CAIWU"} {
        give cai wu to @getenv{hb_owner};
    };
    #elseif {"${hb_state}" == "ENDED"} {
        ask @getenv{hb_owner} about finish;
    };
    #elseif {"${hb_state}" == "GOT_MIXIN"} {
        give mixin to @getenv{hb_owner};
        xian mixin;
    };
    #elseif {"${hb_state}" == "nil"} {
        show_to_info 没有推车任务;
    };
    #else {
        hb.f;
        #if {${hb_auto} == 1} {
            hb.start;
        };
    };
};

#alias {hb.q} {
    #var getesc_current_id 0;
    #if {"@getenv{hb_state}" == "nil"} {
        #if {"%1" != ""} {
            getesc %1;
        };
        #else {
            #ticker {hb_q_ticker} {
                listesc;
            } {5};
        };
    };
};

#alias {hb.f} {
    @setenv{hb_lunci;0};
    @setenv{hb_lunci_total;0};
    @setenv{hb_state;nil};
    @setenv{hb_mixin_lunci;6};
    hb.teardown;
    ask @getenv{hb_owner} about fail;
};

#alias {hb.b} {
    hb.teardown;
    gt @getenv{hb_owner_loc};
    on_there {
        hb.d;
    };
};

#alias {hb.back} {
    #alias tmp_hb_back #cr;
    #alias tmp_hb_back %1;
    gt @getenv{hb_owner_loc};
    on_there {
        tmp_hb_back;
    };
};

#alias {hb.start} {
    log.start;
    set_last_quest hb;
    set brief 2;
    #var hb_auto 1;
    #action {转头对你道：「辛苦了，有几两银子略表心意，已经吩咐人存入你的钱庄账户。」} {
        #if {"@getenv{hb_state}" == "ENDED"} {
            @setenv{hb_state;nil};
            #delay {hb_auto_delay} {
                #if {${hb_auto} == 1} {
                    hb.restart;
                };
            } {3};
        };
    };

    #action {递给你一封密信} {
        @setenv{hb_state;GOT_MIXIN};
        @setenv{hb_lunci_total;@eval{@getenv{hb_lunci_total}+1}};
        @setenv{hb_lunci;0};
        show_to_tuiche 密信;
        xian mixin;
    };

    #action {你给%*一些失踪的财物} {
        show_to_tuiche 密信完成;
        @setenv{hb_state;nil};
        @setenv{hb_mixin_lunci;5};
        #delay {hb_auto_delay} {
            #if {${hb_auto} == 1} {
                hb.restart;
            };
        } {3};
    };

    #class hb.auto.inner open;
    #alias {on_severe_wound} {
        keyao {
            hb.b;
        };
    } {1};

    #action {^你不是才要了任务吗} {
        hb.f;
        hb.restart;
    };

    #action {你一共被奖励了：} {
        #class tmp.inner open;
        #action {%*点经验；} {
            #class tmp.inner kill;
            show_to_info @ctd{%%%1} 点经验;
        };
     #class tmp.inner close;
    };

    #action {${char_name}帮%*追回财物，额外获得%*点经验} {
        show_to_info 额外获得 @ctd{%%2} 点经验;
    };

    #alias {got_gem} {
        #action {你将手中%*{◎|☆|★}%*放进了宝石袋} {
            gem.record %%%1 %%%2 %%%3;
        };
        pack gem;
        #delay {pack_gem} {
            #unaction {你将手中%*{◎|☆|★}%*放进了宝石袋};
        } {3};
    };

    #action {哇，你太厉害了，这么快就把这些财物追回，我有重谢！} {
        got_gem;
    };

    #action {掉了出来一颗} {
        got_gem;
    };

    #action {%d%s%S%s%S%s待认领} {
        #var getesc_id %%1;
        #var getesc_address %%3;
        #if {${getesc_current_id} == 0 && @should_accept_quest{${getesc_address}} == 1} {
            #var getesc_current_id ${getesc_id};
            #var getesc_current_address ${getesc_address};
        };
    };

    #action {来认领押镖任务} {
        #var hb_state @getenv{hb_state};
        #if {"${hb_state}" == "nil"} {
            #if {${getesc_current_id} != 0} {
                #var hb_rotate_location_count 0;
                hb.q ${getesc_current_id};
            };
            #else {
                #math hb_rotate_location_count ${hb_rotate_location_count} + 1;
                #if {${hb_rotate} == 1 && ${hb_rotate_location_count} > 3 && "${hb_rotate_location}" != "nil"} {
                    #var hb_rotate_location_count 0;
                    gt ${hb_rotate_location};
                    on_there {
                        hb.restart;
                    };
                };
            };
        };
    };

    #action {%*向%*打听有关%*的消息} {
        #if {"%%1" == "你"} {
            #var hb_requested 1;
        };
        #else {
            #var hb_requested 0;
        };
    };

    #action {%*说道：「镖车没有送到地头，莫非是你吞了！？」} {
        #if {${hb_requested} == 1} {
            hb.f;
            hb.start;
        };
    } {1};

    #action {${char_name}把这批红货送到%*他已经派了个伙计名叫%*到%*附近接你} {
        #unticker {hb_q_ticker};
        @setenv{hb_state;STARTED};
        @setenv{hb_lunci;@eval{@getenv{hb_lunci}+1}};
        @setenv{hb_huoji;%%2};
        #nop INVOKE gps.get_area %%1;
        #nop #var hb_dest ${gps.area}%%3;
        #var hb_dest ${getesc_current_address};
        @setenv{hb_dest;${hb_dest}};
        #var hb_info %%2|${hb_dest};
        @setenv{hb_info;${hb_info}};
        show_to_tuiche ${hb_info};
        autopp.set %%2;
        hb.setup;
        hb.resume;
    } {1};

    #class hb.auto.inner close;

    @declarenv{hb_state;nil};
    @declarenv{hb_mixin_lunci;6};
    @declarenv{hb_lunci;0};
    @declarenv{hb_lunci_total;0};

    #var hb_state @getenv{hb_state};
    #var hb_location %1;
    #if {"${hb_location}" == ""} {
        #var hb_location @getenv{hb_owner_loc};
    };
    #if {"${hb_state}" == "STARTED" || "${hb_state}" == "TRAVERSE" } {
        #var hb_huoji @getenv{hb_huoji};
        #var hb_dest @getenv{hb_dest};
        #var hb_info @getenv{hb_info};
        #if {"${hb_huoji}" == "nil" || "${hb_dest}" == "nil"} {
            show_to_info 推车失败;
            hb.f;
        };
        #else {
            show_to_info 继续推车;
            show_to_tuiche ${hb_info};
            autopp.set ${hb_huoji};
            #var hb_remaining_path_size 0;
            hb.setup;
            #delay {hb_delay} {
                hb.resume;
            } {5};
        };
    };
    #elseif {"${hb_state}" == "GOT_MIXIN"} {
        notify 有密信;
    };
    #elseif {"${hb_state}" == "nil"} {
        #if {"${hb_location}" == "nil"} {
            show_to_info 请指定镖局: zz, zuo, tf, fh, fm;
            hb.stop;
        };
        #else {
            chain fullme hb.p {gt ${hb_location};on_there {hb.get_owner {hb.restart;};};};
        };
    };
    #else {
        chain fullme hb.b;
    };
};

#alias {hb.restart} {
    drop_garbage;
    #math next_lunci @getenv{hb_lunci}+1;
    show_to_tuiche 准备;
    on_health {hb.q};
};

#alias {hb.stop} {
    log.stop;
    unset_last_quest;
    #var hb_auto 0;
    health.cancel;
    hb.teardown;
    #class hb.auto.inner kill;
    #undelay {hb_auto_delay};
    #unticker {hb_q_ticker};
    #unticker {hb_ticker};
    @clearenv{hb_dest};
    @clearenv{hb_huoji};
    @clearenv{hb_info};
    @clearenv{hb_owner};
    @clearenv{hb_state};
    show_to_tuiche 推车结束;
};

#alias {mx} {
    chain on_there mx.q;
    gt %1;
};

#alias {mx.q} {
    #class mx.inner open;
    #ticker {mx_q} {
        zhaoxun;
    } {5};
    #action {你扯开嗓子，开始大喊：小王八蛋，你给我滚出来} {
        #class mx.inner kill;
        notify 密信地点不对;
    };
    #action {一个伙计挖着鼻屎走了出来，道：你找我啥事} {
        #unticker {mx_q};
        #delay {3} {
            gps.traverse 财物的伙计 bfs;
        };
    };
    #action {GPS: 遍历完成} {
        #class mx.inner kill;
        notify 没找到伙计;
    };
    #action {找到目标} {
        mx.check;
    };
    #alias {mx.got_caiwu} {
        @setenv{hb_state;GOT_CAIWU};
        show_to_tuiche 拿到财宝;
        play get_item.wav;
        #class mx.inner kill;
        hb.b;
    };
    #alias {mx.check} {
        l;
        #class mx.inner open;
        #action {财物的伙计 %S(%S)} {
            #format {tmp_huoji} {%l} {%%%2};
            #class mx.inner open;
            #action {把一包财物砸向你，一转眼不见了。} {
                #undelay {mx_delay};
                mx.got_caiwu;
            };
            #delay {mx_delay} {
                mx.got_caiwu;
            } {10};
            #ticker {yao} {
                ask $tmp_huoji about yao;
            } {1};
            #class mx.inner close;
        } {1};
        #class mx.inner close;
    };

    #class mx.inner close;
    zhaoxun;
};

#alias {hb.p} {
    #alias tmp_hb_p #cr;
    #alias tmp_hb_p %1;
    show_to_tuiche 准备;
    #var chain_action_hb_p sell.all get.qian get.san get.bao jianding.all donate.all repair special_repair gem.combine hb.spec tmp_hb_p;
    #if {${need_reload} == 1} {
        #var chain_action_hb_p reload ${chain_action_hb_p};
    };
    chain ${chain_action_hb_p};
};

#alias {show_to_tuiche} {
    show_to_quest 推车: @getenv{hb_lunci_total}+@getenv{hb_lunci}: %0;
};

#alias {hb.check_robber} {
    #delay {detect_enemy_delay} {
        #var hb_robber_count ${tmp_robber_count};
        #class detect_enemy.inner kill;
    } {1};
    #math tmp_robber_count ${tmp_robber_count}+1;
    #var tmp_score @mapget{robber_score;%1};
    #if {${tmp_score} > ${robber_score}} {
        #var robber_score ${tmp_score};
        #var enemy_menpai %1;
        #var enemy ${char_id}'s robber ${tmp_robber_count};
    };
};

#alias {hb.resume} {
    #var hb_state @getenv{hb_state};
    #if {"${hb_state}" == "STARTED"} {
        hb.goto_dest_area;
    };
    #elseif {"${hb_state}" == "TRAVERSE"} {
        hb.traverse;
    };
};

#alias {hb.back_and_forth} {
    #alias {get_pos_done} {
        #list exits_list create ${exits_list};
        #var back_exit ${exits_list}[1];
        #list hb_path create {${back_exit};@opposite{${back_exit}}};
        #var hb_path_index 1;
        #list hb_path size hb_path_size;
        hb.run;
    };
    get_pos {
        get_pos_done;
    };
};

#alias {hb.goto_dest_area} {
    #var hb_paused 0;
    #alias {gps_check_room_done} {
        #if {${gps.roomno} == -1} {
            #delay {1} {
                #show {GPS: 当前房间未记录, 无法行走};
            };
        };
        #else {
            #math hb_remaining_path_size ${hb_remaining_path_size}-${hb_path_index};
            #var gps.from_roomno ${gps.roomno};
            INVOKE gps.get_room ${hb_dest};
            #if {${gps.from_roomno} == ${gps.roomno}} {
                hb.back_and_forth;
                #return;
            };
            INVOKE gps.get_path ${gps.from_roomno} ${gps.roomno} ${HB_GPS_WEIGHT};
            #var hb_path ${gps_path};
            #var hb_path_index 1;
            #list hb_path size hb_path_size;
            #if {${hb_path_size} == 0} {
                #show {GPS: 目标房间不可达, 无法行走: ${hb_dest}:${gps.roomno}};
                #return;
            };
            #if {@getenv{hb_mixin_lunci} == 5} {
                #math additional_path_size ${hb_remaining_path_size} - ${hb_path_size};
                #if {${additional_path_size} > 0} {
                    #list hb_path get 3 first_step;
                    #var second_step @opposite{${first_step}};
                    #if {"${second_step}" != "nil"} {
                        #var cnt 0;
                        #while {${cnt} < ${additional_path_size}} {
                            #list hb_path insert 3 ${second_step};
                            #list hb_path insert 3 ${first_step};
                            #math cnt $cnt+2;
                        };
                    };
                    #list hb_path size hb_path_size;
                };
            };
            hb.run;
        };
    };
    gps.check_room {
        gps_check_room_done;
    };
};

#alias {hb.detect_enemy} {
    #class detect_enemy.inner open;
    #action {~你想要收[1;31m劫匪[2;37;0m为弟子} {
        hb.check_robber quanzhen;
    };
    #action {~你想要收[1;32m劫匪[2;37;0m为弟子} {
        hb.check_robber shaolin;
    };
    #action {~你想要收[1;33m劫匪[2;37;0m为弟子} {
        hb.check_robber gaibang;
    };
    #action {~你想要收[1;34m劫匪[2;37;0m为弟子} {
        hb.check_robber xingxiu;
    };
    #action {~你想要收[1;35m劫匪[2;37;0m为弟子} {
        hb.check_robber huashan;
    };
    #action {~你想要收[1;36m劫匪[2;37;0m为弟子} {
        hb.check_robber taohua;
    };
    #action {~你想要收[1;37m劫匪[2;37;0m为弟子} {
        hb.check_robber wudang;
    };
    #class detect_enemy.inner close;

    #var enemy nil;
    #var tmp_robber_count 0;
    #var hb_robber_count 0;
    #var robber_score 0;
    shou ${char_id}'s robber 1;
    shou ${char_id}'s robber 2;
    shou ${char_id}'s robber 3;
    shou ${char_id}'s robber 4;
    #nop ]]]]]]]]]]]]]];
};

#list hb_blacklist_baowei:荆西镖局 create {
    武当三清殿;少林佛心井;扬州醉仙楼;襄阳当铺;
};

#list hb_blacklist:荆西镖局 create {
    全真天尊讲经堂;
};

#list hb_blacklist:飞马镖局 create {
    晋阳萧府;成都当铺;赞普来福杂货;大理太和居;武当荆西镖局;昆明神威镖局;全真天尊讲经堂;
};

#list hb_blacklist_baowei:飞马镖局 create {
    襄阳当铺;武当荆西镖局;
};

#list hb_blacklist:飞虎镖局 create {
    晋阳萧府;赞普来福杂货;
};

#list hb_blacklist_baowei:飞虎镖局 create {
    武当荆西镖局;
};

#list hb_blacklist:天福镖局 create {
    长安飞马镖局;赞普来福杂货;峨嵋毗卢殿;
};

#list hb_blacklist_baowei:天福镖局 create {
    武当荆西镖局;襄阳当铺;
};

#list hb_blacklist:昌隆镖局 create {
    归云庄太湖街;峨嵋毗卢殿;苏州聚宝斋;
};

#class hubiao close
